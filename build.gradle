plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'org.beryx.jlink' version '2.24.1'
    id 'edu.sc.seis.launch4j' version '2.4.7' // facultatif
}

group 'com.rafaros'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.8.2'
    javafxSdk = 'C:/javafx/javafx-sdk-11.0.2'
}

sourceCompatibility = '11'
targetCompatibility = '11'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

javafx {
    version = '11.0.2'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.web', 'javafx.media']
}

dependencies {
    implementation 'org.controlsfx:controlsfx:11.1.1'
    implementation 'com.dlsc.formsfx:formsfx-core:11.3.2'
    implementation 'net.synedra:validatorfx:0.3.1'
    implementation 'org.kordamp.ikonli:ikonli-javafx:12.3.0'
    implementation 'org.kordamp.bootstrapfx:bootstrapfx-core:0.4.0'
    implementation 'eu.hansolo:tilesfx:11.48'
    implementation 'com.github.almasb:fxgl:11.17'
    implementation 'org.apache.commons:commons-imaging:1.0-alpha2'

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

application {
    mainClass = 'com.rafaros.filemanagerutils.FileManagerUtils'
    mainClassName = 'com.rafaros.filemanagerutils.FileManagerUtils'
}

tasks.withType(Test) {
    useJUnitPlatform()
}

shadowJar {
    archiveBaseName.set('FileManagerUtils')
    archiveClassifier.set('')
    archiveVersion.set('')
    manifest {
        attributes(
                'Main-Class': application.mainClass
        )
    }
    mergeServiceFiles()
}

tasks.build {
    dependsOn shadowJar
}

// -----------------------------
// Installer Windows avec jpackage
// -----------------------------
tasks.register('jpackageInstaller', Exec) {
    group = "distribution"
    description = "Génère l'installateur Windows (.exe)"

    dependsOn shadowJar

    commandLine(
            "C:/Program Files/Java/jdk-17/bin/jpackage.exe",

            "--type", "exe",
            "--name", "FileManagerUtils",

            "--input", "${buildDir}/libs",
            "--main-jar", "FileManagerUtils.jar",
            "--main-class", "com.rafaros.filemanagerutils.FileManagerUtils",

            // Java options
            "--java-options", "-Xmx256m",

            // Module-path pour JavaFX
            "--module-path", "${javafxSdk}/lib",
            "--add-modules", "javafx.controls,javafx.fxml,javafx.web,javafx.media",

            "--icon", "src/main/resources/logo/File-Manager.ico",
            "--dest", "${buildDir}/installer",

            "--win-menu",
            "--win-shortcut",

            "--vendor", "Rafaros",
            "--app-version", "1.0.0"

            // "--win-console" // Décommenter pour debug si l'application ne s'ouvre pas
    )
}

//----------------------------------------------------/
//fenêtre gradle de droite :
//----------------------------------------------------/
//          build=>clean
//          build=>build
//----------------------------------------------------/
//Pour lancer l'application (mode dev) :
//----------------------------------------------------/
//          application=>run
//----------------------------------------------------/
//pour produire le jar executable installeur :
//----------------------------------------------------/
//          distribution=>jpackageInstaller